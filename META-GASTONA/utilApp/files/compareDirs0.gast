#javaj#

   <sysDefaultFonts>
      <!Tahoma, 12, 1, *
      Consolas, 12, 0, TextArea.font

   <frames>
      F      , Compare Directories, 600, 600
      Fbatch , Batch

   <layout of F>
      EVA, 10, 10, 5, 5

         ---, X           , X
            , lDirLeft    , lDirRight
          22, eDirLeft    , eDirRight
            , botones     , -
            , lSolEsquerra, lSolDereta
          X , tSolEsquerra, tSolDereta
            , lIguals     , lDiferents
          X , tIguals     , tDiferents
            ,             , bDiffFiles

   <layout of botones>
      EVA,,, 3, 3

      ---,             , X       , X
         , kDontUseDate, bCompare, bGenBatch

   <layout of Fbatch>
      EVA, 8, 8, 3, 3

      ---, X
       X , xBatch

#data#

   <eDirLeft>
   <eDirRight>

   <kDontUseDate> Do not use date to determine identic files
   <kDontUseDate selected> 0

   <tDiferents visibleColumns> subPath
   <tIguals visibleColumns> subPath
   <tSolEsquerra visibleColumns> subPath
   <tSolDereta visibleColumns> subPath


#listix#

   <-- bGenBatch>
      GEN, @<tmp1>, generateBatch
      SETVAR, xBatch fileName, @<tmp1>
      MSG, xBatch load
      JAVAJ, FRAME, Fbatch, 1


   <-- tDiferents>
      -->, bDiffFiles control!, enabled, 1

   <-- bDiffFiles>
      CHECK, VAR, tool diffFiles, @<tool Diff required>
      CHECK, VAR, tDiferents selected.subPath, -->, bDiffFiles, enabled, 0
      LAUNCH, //@<tool diffFiles> "@<eDirRight>\@<tDiferents selected.subPath>" "@<eDirLeft>\@<tDiferents selected.subPath>"


   <tool Diff required>
      BOX, I, //Requires <tool diffFiles>, if you have such a tool in your machine installed, just give it in @<:sys user.home>\.gastonaLocal file


   <-- kDontUseDate>
      @<updateIgualsAndDiff>

   <dateComma>          IF, @<kDontUseDate selected>, =, 0, //date,
   <differentDateOR>    IF, @<kDontUseDate selected>, =, 0, //left_all.date != right_all.date OR


   <db> ""

   <main0>
      SET VAR, tmp1, @<:lsx newTempFile>
      LOAD, data, @<:sys user.home>/.gastonaLocal

      CHECK, VAR, p1
      SETVAR, eDirLeft, @<p1>

      CHECK, VAR, p2
      SETVAR, eDirRight, @<p2>

      <! dir given, Launch comparison
      @<do compare dirs>

   <-- bCompare>
      @<do compare dirs>

   <do compare dirs>
      DATABASE, @<db>, EXECUTE, //DELETE FROM left_files;
      DATABASE, @<db>, EXECUTE, //DELETE FROM right_files;
      SCAN, ADDFILES, @<db>, @<eDirLeft>
          , PREFIX  , "left"

      SCAN, ADDFILES,@<db>, @<eDirRight>
          , PREFIX  , "right"

      @<updateIgualsAndDiff>

      -->, tSolEsquerra, sqlSelect, @<sql OnlyLeftFiles>
      -->, tSolDereta  , sqlSelect, @<sql OnlyRightFiles>

   <generateBatch>
      //ECHO ONLY LEFT FILES (copy them to right)
      //ECHO =====================================
      //
      LOOP, SQL,, @<sql OnlyLeftFiles>
         ,, //COPY "@<eDirLeft>\@<subPath>" "@<eDirRight>\@<subPath>"
      //
      //
      //ECHO ONLY RIGHT FILES (delete them from right side)
      //ECHO ================================================
      //
      LOOP, SQL,, @<sql OnlyRightFiles>
         ,, //DEL "@<eDirRight>\@<subPath>"
      //
      //
      //ECHO DIFFERENT FILES (copy them to right)
      //ECHO ====================================
      //
      LOOP, SQL,, @<sql DifferentFiles>
         ,, //COPY "@<eDirLeft>\@<subPath>" "@<eDirRight>\@<subPath>"
      //
      //
      //ECHO IDENTIC FILES (delete from left)
      //ECHO ====================================
      //
      LOOP, SQL,, @<sql IdenticFiles>
         ,, //DEL "@<eDirLeft>\@<subPath>"


   <updateIgualsAndDiff>
      -->, tIguals     , sqlSelect, @<sql IdenticFiles>
      -->, tDiferents  , sqlSelect, @<sql DifferentFiles>

   <sql OnlyLeftFiles>
      // SELECT * FROM left_all LEFT JOIN right_all
      //    USING (subPath)
      //    WHERE right_all.fileName IS NULL;

   <sql OnlyRightFiles>
      // SELECT * FROM right_all
      //    LEFT JOIN left_all USING (subPath)
      //    WHERE left_all.fileName IS NULL;

   <sql IdenticFiles>
      // SELECT *
      //    FROM left_all, right_all
      //    USING (subPath, @<dateComma> size);

   <sql DifferentFiles>
      // SELECT *
      //    FROM left_all, right_all
      //    USING (subPath)
      //    WHERE (@<differentDateOR> left_all.size != right_all.size);

#**#
