#gastona#

   <fusion>
      META-GASTONA/utilApp/std/ExportBasicDialog.gasti
      META-GASTONA/utilApp/arces/gasti/sqliteHelpData.gasti
      META-GASTONA/utilApp/arces/gasti/sqliteHelpGUI.gasti
      META-GASTONA/utilApp/arces/gasti/markselector.gast

#javaj#

   <frames>
      Fmain, ARCess mako3 v0.8.20200618, 920, 650
      FmarkSelector, Mark selector v0.1

   <sysDefaultFonts>
      Tahoma, 11, 1, *
      Consolas, 14, 0, TextArea.font

   <layout of Fmain>
      EVA, 5, 5, 5, 5

        ---,        ,  X
           , cabeza ,  -
         10,
         X , esplata, -

   <layout of esplata>
      SPLIT, X, framilia

      ristra, tabas

   <layout of cabeza>
      EVA, 0, 0, 5, 5

       ZZZ,               ,   X      ,             ,
          ,  lDatabaseName, eDBName  ,  bChooseFile, gLuces
          ,  lSQLQuery    , eSQLQuery,  bRun       , kMarks

   <layout of tabas>
      TABBED,

      Table view, table with export
      Sql Assit , sqlAssist
      Schema    , xSchema
      Console   , layConsoles

      
   <layout of layConsoles>
       EVA, 10, 10, 5, 5
       --,    A   , X
         , lStandard output
       X , o1Consola, -
         , lErrors
       X , o2Consola, -
         , bClear Consoles
      
   <layout of ristra>
      EVA

        ZZZ, 150, X,
           , layFilterTabCol, -
         X , aDeepColumns, -

   <layout of table with export>
      EVA, 5, 5, 5, 5

         , X
       X , sTabala
         , layExportBasic

   <layout of layFilterTabCol>
      EVA, 5, 5, 5, 5

         ,             , X
         , lSchema filt, eFilterTabVie, kSort

   <MessageToMessage>

      _lib db.sqlite.callStart             , gLuces L12
      _lib db.sqlite.callEnd               , gLuces L11
      _lib db.sqlite.callError             , gLuces L13

   <sysDefaultImages>
      javaj/img/column.gif, Tree.leafIcon
      javaj/img/table.gif, Tree.rootIcon, Tree.secondRootIcon
      javaj/img/tableconnection.gif, Tree.openIcon, Tree.closedIcon

#data#

   <xSchema fileName> :mem xschemafile

   <aDeepColumns rootTitle> Deep columns
   <aDeepColumns separator> " "

   <eDBName>

   <bChooseFile>     Selec...

   <bChooseFile Dialog> FILE
   <bChooseFile widget> eDBName


#listix#

   <DBG(m)> UDP, SEND ONCE, localhost, 18881, @<p1>

   <main>
      @<-- bClear Consoles>
      <! === evaluateArguments
      IN CASE, @<:sys gastona.argumentCount>
             , 0    , -->, eDBName data!,, ""
             , ELSE , -->, eDBName data!,, @<p1>
      LSX, update mainDatabase
      CALL CAPTURE, @<:mutool sqlite>, .version


   <sql tablas> //SELECT type,name FROM sqlite_master WHERE (type='table' OR type='view') AND NOT name LIKE 'sqlite%'
   <! AND NOT name LIKE "__dbMore%";

   <-- kSort>          LSX, update deepTree
   <-- eFilterTabVie>  LSX, update deepTree

   <-- kMarks>
      CHECK, =, @<kMarks selected>, 1, -->, FmarkSelector hide
      
      LSX, markSelectorRefresh
      -->, Fmain tellPosAndSize
      NUM=, FmarkSelector posX, @<Fmain posX> + @<Fmain sizeX>
      NUM=, FmarkSelector posY, @<Fmain posY>
      NUM=, FmarkSelector sizeY, @<Fmain sizeY>
      NUM=, FmarkSelector sizeX, 200
      -->, FmarkSelector control!
      -->, FmarkSelector show
   

   <update deepTree>
      MSG, aDeepColumns data!, sqlSelect, //SELECT deepColumn FROM dbtemp.theDBDeepSchema WHERE deepColumn LIKE '%@<:encode eFilterTabVie>%' @<ORDER_OR_NOT>

   <ORDER_OR_NOT>
      CHECK, VAR, kSort selected
      IN CASE, @<kSort selected>
             , 1, // ORDER BY deepColumn

   <update mainDatabase>
      CHECK, !=, @<eDBName>, ""
      VAR=, tmpDB, @<:lsx tmp db>
      DB CONFIG, DEFAULT, @<eDBName>
      -->, sLasTablas data!, sqlSelect, //@<sql tablas>

      <! print schema formated
		DB,, SCHEMA, theDBSchema
      DBMORE, LOAD CONNECTIONS,, theDBConnections, 1
		DBMORE, DEEP SCHEMA, theDBDeepSchema, theDBSchema, theDBConnections
      DB, @<tmpDB>, CREATE TABLE, theDBDeepSchema
      DB CONFIG, ALIAS, dbtemp, @<tmpDB>
      LSX, update deepTree

      <!=== update schema text widget xSchema
      <!
      GEN, @<xSchema fileName>, print schema (db), listix, META-GASTONA/utilApp/std/printSchema.lsx
         , PARAMS, @<eDBName>
      MSG, xSchema load

   <-- bClear Consoles>
       MSG, o1Consola clear
       MSG, o2Consola clear
   
   <-- eDBName>
      LSX, update mainDatabase

   <-- bRun>
      @<-- eSQLQuery>

   <-- eSQLQuery>
      -->, sTabala data!, sqlSelect,      //@<eSQLQuery>

   <-- sLasTablas>
      -->, eSQLQuery data!, ,             //SELECT * FROM @<sLasTablas selected.name>
      -->, sTabala data!, sqlSelect,      //SELECT * FROM @<sLasTablas selected.name>

   <-- aDeepColumns>
      CHECK, VAR, aDeepColumns selectedPath
      CHECK, !=, "", @<aDeepColumns selectedPath>
      -->, UPDATE_SQLGRID, @<sql deep>

    <-- aDeepColumns parent>
      <! --- try "SELECT * FROM xxx"
      LOOP, SQL,, //SELECT name FROM sqlite_master WHERE type IN ('table', 'view') AND name = '@<aDeepColumns selectedPath>' LIMIT 1
          ,, -->, UPDATE_SQLGRID, //SELECT * FROM @<aDeepColumns selectedPath>

   <-- UPDATE_SQLGRID>
      -->, eSQLQuery data!, ,@<p1>
      -->, sTabala data!, sqlSelect, @<p1>
      LSX, ExportBasic set (gridName dbName), sTabala, ""

   <sql deep>
      DBMORE, TOSQL, aDeepColumns selectedMultiPath
            , VARCONNECTIONS, theDBConnections
            , DEEP COL HEADER, 1
            , LINE BREAK, 0